class FrontEndEditor.fieldTypes.group extends FrontEndEditor.fieldTypes.input
	($container, editors) ->
		@el = $container
		@editors = editors

		@has_aloha = false

		if GENTICS?.Aloha
			for editor of @editors
				if 'rich' == editor.get_type()
					@has_aloha = true
					return

	start_editing: (ev) ->
		@create_form()

		if @has_aloha
			GENTICS.Aloha.wpSaveCancel.current_field = this
		else
			@create_buttons()

		@ajax_get()

		false

	create_form: ->
		for editor of @editors
			editor.create_form()
			editor.create_input()

		super ...

		@el.append @form

	remove_form: (ev) ->
		for editor of @editors
			editor.remove_form()

		super ...

	content_from_input: ->
		(editor.content_from_input() for editor of @editors)

	keypress: jQuery.noop

	ajax_set: ->
		super ...

		FrontEndEditor.overlay.cover @el

	ajax_args: ->
		args = super ...

		args.group = true

		dataArr = (editor.data for editor of @editors)

		if dataArr.length == 1
			args.data = dataArr
		else
			# copy all properties from first field
			commonData = {...dataArr[0]}

			# keep only common props
			for i from 1 to dataArr.length-1
				for own key, value in commonData
					if value !== dataArr[i][key]
						delete commonData[key]

			# construct new dataArr with only the distinct props
			args.data =
				for data of dataArr
					item = {}
					for own key in data
						if key not in commonData
							item[key] = data[key]
					item

			args.commonData = commonData

		args

	ajax_get_handler: (response) ->
		for editor, i of @editors
			editor.ajax_get_handler response[i]

		@editors[0].input?.focus()

	ajax_set_handler: (response) ->
		for editor, i of @editors
			editor.ajax_set_handler response[i]

		@remove_form()
