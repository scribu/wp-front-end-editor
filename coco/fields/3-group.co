class FrontEndEditor.fieldTypes.group extends FrontEndEditor.fieldTypes.input
	editors: [],

	start: ->
		@has_aloha = false

		if GENTICS.Aloha?
			for editor of @editors
				if 'rich' == editor.get_type()
					@has_aloha = true
					return

	start_editing: (ev) ->
		@form_create()

		if @has_aloha
			GENTICS.Aloha.wpSaveCancel.group = this
		else
			@buttons_create()

		@ajax_get()

		false

	form_create: ->
		for editor of @editors
			editor.form_create()
			editor.input_create()

		super ...

		@el.append(@form)

	form_remove: (ev) ->
		for editor of @editors
			editor.form_remove()

		super ...

	content_from_input: ->
		editor.content_from_input() for editor of @editors

	keypress: jQuery.noop

	ajax_set: ->
		super ...

		FrontEndEditor.overlay.cover(@el)

	ajax_args: (args) ->
		args = super ...

		jQuery.extend args, {
			group : true,
			data  : (editor.data for editor of @editors)
		}

		args

	ajax_get_handler: (response) ->
		for editor, i of @editors
			editor.ajax_get_handler(response[i])

		@editors[0].input?.focus()

	ajax_set_handler: (response) ->
		for editor, i of @editors
			editor.ajax_set_handler(response[i])

		@form.remove()

		GENTICS.Aloha.wpSaveCancel.group = void
