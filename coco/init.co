jQuery ->

	# Text widget fields hack
	jQuery('[data-filter="widget_title"], [data-filter="widget_text"]').each ->
		$el = jQuery(this)
		$widget = $el.closest('.widget_text')

		if $widget.length
			$el.attr('data-widget_id', $widget.attr('id'))
			$widget.addClass('fee-group')
		else
			$el.unwrap()

	# Create group instances
	jQuery('.fee-group').each ->
		$container = jQuery(this)
		$elements = $container.find('.fee-field').removeClass('fee-field')

		if !$elements.length
			return

		editors = (FrontEndEditor.make_editable(el) for el of $elements)

		editor = new FrontEndEditor.fieldTypes['group'] $container, editors

		if FrontEndEditor.data.add_buttons
			$button = jQuery '<span>', {
				class: 'fee-edit-button'
				text: FrontEndEditor.data.edit_text
			}

			$button.appendTo $container
		else
			$button = $container.find '.fee-edit-button'
			if not $button.length
				$button = $container.find '.post-edit-link'

		if $button.length
			$button.click editor.~start_editing

			$container.bind {
				edit_start: (ev) ->
					$button.addClass 'fee-disabled'

					ev.stopPropagation()
				edit_stop: (ev) ->
					$button.removeClass 'fee-disabled'

					ev.stopPropagation()
			}
		else
			FrontEndEditor.hover_init $container, editor.~start_editing

		$container.data 'fee-editor', editor

	# Create field instances
	for el of jQuery('.fee-field')
		FrontEndEditor.make_editable el, true

jQuery(window).load ->
	# Automatically start editing the first auto-draft
	jQuery('.fee-group.status-auto-draft').data('fee-editor').start_editing()
